import edu.wpi.first.gradlerio.frc.FRCPlugin
import edu.wpi.first.toolchain.NativePlatforms

apply plugin: "cpp"
apply plugin: "edu.wpi.first.GradleRIO"
apply plugin: "edu.wpi.first.GradleJni"


// plugins {
//     id "java"
//     id "cpp"
//     id "edu.wpi.first.GradleRIO" version "2019.3.2"
//     id 'edu.wpi.first.GradleJni' version '0.4.1'
// }

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        roboRIO("roborio") {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = frc.getTeamNumber()
        }
    }
    artifacts {
        nativeArtifact('pixy2_spi') {
            targetPlatform = NativePlatforms.roborio
            targets << "roborio"
            directory = FRCPlugin.LIB_DEPLOY_DIR
            buildType = 'release'
        }
    }
}

// Set this to true to enable desktop support.
def includeDesktopSupport = false

// Maven central needed for JUnit
repositories {
    mavenCentral()
}

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 4.
dependencies {
    compile wpi.deps.wpilib()
    compile wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
    testCompile 'junit:junit:4.12'
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

model {
    components {
        pixy2_spi(JniNativeLibrarySpec) { // Use JniNativeLibrarySpec to get a JNI library
            targetPlatform wpi.platforms.roborio
            enableCheckTask true // Set to true to enable a JNI check task. This will search all generated JNI headers, and check to ensure their symbols exist in the native library
            javaCompileTasks << compileJava // set javaCompileTasks to any java compile tasks that contain your JNI classes. It is a list of tasks
            jniCrossCompileOptions << JniCrossCompileOptions(wpi.platforms.roborio)
            
            binaries.all {
                cppCompiler.args '-g', '-D__LINUX__'
            }
            
            sources.cpp {
                source {
                    srcDir 'src/main/jni/cpp'
                    include '**/*.cpp', '**/*.c'
                }
                exportedHeaders {
                    srcDirs 'src/main/jni/include','src/main/jni/include/Pixy'
                    if (project.hasProperty('generatedHeaders')) {
                        srcDir generatedHeaders
                    }
                    include '**/*.h'
                }
            }
            // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
            useLibrary(it, "wpilib")
        }
    }
}
